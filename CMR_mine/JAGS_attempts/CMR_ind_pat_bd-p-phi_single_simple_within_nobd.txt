model {

####
## Priors
####

beta_phi[1]    ~ dnorm(0, 0.368)
beta_phi[2]    ~ dnorm(0, 0.368)
beta_p[1]      ~ dnorm(0, 0.368)
beta_p[2]      ~ dnorm(0, 0.368)
beta_timegaps  ~ dnorm(0, 0.368)

####
## Survival and Detection
####

for (i in 1:n_ind) {

 for (t in 1:n_occ_min1) {
          
  phi[i, t] = ilogit(beta_phi[1] + beta_timegaps * time_gaps[t] + beta_phi[2] * X_bd[i, sampling_events[t]])

 }

 for (t in 1:n_occasions) {

  p[i, t] = ilogit(beta_p[1] + beta_p[2] * X_bd[i, sampling_events[t]])
 
 }

}

####
## Chi
####

for (i in 1:n_ind) {

 chi[i, n_occasions] = 1      

 for (t in 1:n_occ_min1) {  

chi[i, n_occasions - t] = (1 - phi[i, n_occasions - t]) + 
phi[i, n_occasions - t] * 
(1 - p[i, n_occasions - t]) * 
chi[i, n_occasions - t + 1]		

 }
}


####
## Capture and Detection
####

for (i in 1:n_ind) {

 for (t in 2:last[i]) {
  living[i, t] ~ dbern(phi[i, t - 1])   
 }
 for (t in 1:last[i]) {
  y[i, t]            ~ dbern(p[i, t])  
 } 
  chi_restraint[i]   ~ dbern(chi[i, last[i]]);  
}


}

